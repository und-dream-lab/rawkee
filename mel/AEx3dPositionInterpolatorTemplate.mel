//
// Copyright (C) 2004-2005 North Dakota State University (http://atl.ndsu.edu/resources/maya_x3d.php) 
// 
//This library is free software; you can redistribute it and/or 
//modify it under the terms of the GNU Lesser General Public License 
//as published by the Free Software Foundation; either version 2.1 of 
//the License, or (at your option) any later version.

//This library is distributed in the hope that it will be useful, but 
//WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY 
//or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU Lesser General Public 
//License for more details.

//You should have received a copy of the GNU Lesser General Public License 
//along with this library; if not, write to the Free Software Foundation, Inc., 
//59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

// File: AEx3dPositionInterpolatorTemplate.mel
//
//
//
// Authors:	Aaron Bergstrom
//         	Computer Visualization Manger
//         	NDSU Archaeology Technologies Laboratory
//         	http://atl.ndsu.edu/
//

//global string $ftX3DArray[];

global proc AEx3dPositionInterpolatorTemplateNew( string $attrName )
{
	string $nodeName[];
	tokenize($attrName, ".", $nodeName);
	setUITemplate -pst attributeEditorPresetsTemplate;
	if($nodeName[1] == "key"){
		buildKeyAEInterface($nodeName[0], $attrName);
	}

	setUITemplate -ppt;
}

global proc AEx3dPositionInterpolatorTemplateReplace( string $attrName )
{
	string $nodeName[];
	tokenize($attrName, ".", $nodeName);
	if($nodeName[1] == "key")
	{
		deleteUI -lay keyRow1;
		deleteUI -lay keyRow2;
		deleteUI -lay keyRow3;
		buildKeyAEInterface($nodeName[0], $attrName);
	}
}

global proc AEx3dPositionInterpolatorTemplate( string $nodeName )
{
	editorTemplate -beginScrollLayout;
		editorTemplate -beginLayout "Keying Attributes" -collapse false;
			editorTemplate -addControl "position";
			editorTemplate -addControl "keysEverySoOften";
			editorTemplate -addControl "startFrame";
			editorTemplate -addControl "stopFrame";
			editorTemplate -addControl "secondLength";
			editorTemplate -addControl "fps";
		editorTemplate -endLayout;
		editorTemplate -beginLayout "Keys" -collapse false;
			editorTemplate -callCustom "AEx3dPositionInterpolatorTemplateNew" "AEx3dPositionInterpolatorTemplateReplace" "key";
		editorTemplate -endLayout;
		editorTemplate -suppress "key";
		editorTemplate -suppress "keyValue";
		editorTemplate -aec -ecl "Extra Attributes";
	x3dOptionSuppression();
	editorTemplate -endScrollLayout;
}

global proc	buildKeyAEInterface(string $nodeName, string $fullAttr)
{
	float $stpFrame = `getAttr ($nodeName+".stpf")`;
	float $strFrame = `getAttr ($nodeName+".strf")`;
	float $eso = `getAttr ($nodeName+".keso")`;
	
	float $keyFLen = ($stpFrame - $strFrame)/$eso;
	
	int $keyLen = $keyFLen + 1;
	
	if($keyLen == 1) $keyLen = $keyLen+1;
	
	int $i;
	float $tKey[];
	for($i=0;$i<$keyLen;$i++) $tKey[$i] = `getAttr ($nodeName+".key["+$i+"]")`;
	
	rowLayout
		-nc 3
		-columnWidth3 100 250 100
		-cl3 "left" "center" "left" keyRow1;
		text -l " ";
		button -l "Update Keys" -c ("updatePIKeyAtts "+$nodeName);
		text -l " ";
	setParent ..;
	
	rowLayout
		-nc 3
		-columnWidth3 10 400 40
		-cl3 "left" "left" "left" keyRow2;
		text -l " ";
		text -l " ";
		text -l " ";
	setParent ..;
	
	rowLayout
		-nc 3
		-columnWidth3 10 400 40
		-cl3 "left" "left" "left" keyRow3;
		text -l " ";
		scrollField -ww true -en false -w 400 -h 100 meScroll;
		text -l " ";
	setParent ..;
	string $oldString = `scrollField -q -tx meScroll`;
	for($i=0;$i<$keyLen;$i++)
	{
		scrollField -e -tx $tKey[$i] meScroll;
		string $nString = `scrollField -q -tx meScroll`;
		if($i!=0) $oldString = $oldString+", ";
		$oldString = $oldString + $nString;
		scrollField -e -tx $oldString meScroll;
	}
}

//---------------------------------------------------------------------------------------
//Procedure for updating the key and keyValue attributes within the AE only
//---------------------------------------------------------------------------------------
global proc updatePIKeyAtts(string $nodeName)
{
	evalDeferred("resetAE");
}
